# $Id: PKGBUILD 183685 2013-04-26 16:04:28Z heftig $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer
pkgver=1.0.7
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2' 'glib2')
optdepends=('sh: feedback script')
makedepends=('intltool' 'pkgconf' 'gtk-doc' 'gobject-introspection')
options=('!libtool')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz)
sha256sums=('68cada7ee24ede23e15dc81ccde11898eed1a7a3c6a2d81a8c31596fccb1b5ce')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
 
  export PATH="/usr/local/bin:$PATH"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer (ArchBSD)" \
    --with-package-origin="http://www.archbsd.net/" \
    --disable-static
  gmake
}

check() {
  cd "${srcdir}/gstreamer-${pkgver}"

  #Unexpected error from C library during 'pthread_mutex_unlock': Operation not permitted.
  gmake check || true 
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  gmake DESTDIR="${pkgdir}" install
}
